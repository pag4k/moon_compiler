// ALL 5.1, 5.3 (except input) and 5.4

class object {
  integer n1;
  integer id;
  integer n2;
};

class world: object{
  integer a;
  rgb pixels[5][5][5];
  rgb default;
  integer b;
  rgb getAtPos(pos pos);
  integer setAtPos(rgb rgb, pos pos);
  pos findBluest();
};

class rgb {
  integer r;
  integer g;
  integer b;
};

class pos: object {
  integer coord[3];
  integer maxCoord();
  integer getid();
};

// 5.4.6 objects, access to the members of a superclass
integer pos::getid() {
  return(id);
};

rgb world::getAtPos(pos pos) {
  return(pixels[pos.coord[0]][pos.coord[1]][pos.coord[2]]);
};

integer world::setAtPos(rgb rgb, pos pos) {
  pixels[pos.coord[0]][pos.coord[1]][pos.coord[2]] = rgb;
  return(0);
};

// 5.3.3 loop statement: correct implementation of branching mechanism, including for imbricated loop statements
// 5.4.2 arrays of objects, access to an array’s object elements, single or multidimensiona
pos world::findBluest() {
  pos max;
  integer maxb;
  maxb = 0;
  for (integer i = 0; i < 5; i = i + 1)
    for (integer j = 0; j < 5; j = j +1)
      for (integer k = 0; k < 5; k = k +1)
        if (pixels[i][j][k].b > maxb) then {
          maxb = pixels[i][j][k].b;
          max = fromCoord(i, j, k);
        } else {};;;;
  return(max);
};

pos fromCoord(integer x, integer y, integer z) {
  pos new;
  new.coord[0] = x;
  new.coord[1] = y;
  new.coord[2] = z;
  return(new);
};

pos fromArray(integer newCoord[3]) {
  pos new;
  new.coord = newCoord;
  return(new);
};

pos add(pos a1, pos a2) {
  for (integer i = 0; i < 3; i = i +1)
    a1.coord[i] = a1.coord[i] + a2.coord[i];;
  return(a1);
};

rgb newrgb(integer r, integer g, integer b) {
  rgb new;
  new.r = r;
  new.g = g;
  new.b = b;
  return(new);
};

// 5.3.1 assignment statement: assignment of the resulting value of an expression to a variable, independently of what is the complexity of the expression
pos average(pos a[5]) {
  pos ave;
  for (integer i = 0; i < 5; i = i + 1)
    for (integer j = 0; j < 3; j = j +1)
      ave.coord[j] = ave.coord[j] + a[i].coord[j];;;
  for (integer k = 0; k < 3; k = k +1) {
      ave.coord[k] = ave.coord[k] /5;
  };
  return(ave);
};

// 5.3.2 conditional statement: implementation of branching mechanism, including for imbricated conditional statement
// 5.4.1 arrays of basic types (integer and float), access to an array’s elements, single or multidimensional
// 5.4.4 objects, access to members of array types, as well as the elements of the array
integer pos::maxCoord() {
  integer max;
  if (coord[0] >= coord[1]) then
    if (coord[0] >= coord[2]) then
      max = coord[0];
    else
      max = coord[2];;
  else
    if (coord[1] >= coord[2]) then
      max = coord[1];
    else
      max = coord[2];;;
  return(max);
};

main {
  
  pos a[5];
  a[0] = fromCoord(0,1,3);
  a[1] = fromCoord(2,3,2);
  a[2] = fromCoord(2,3,3);
  a[3] = fromCoord(2,4,3);
  a[4] = fromCoord(4,4,4);

  pos ave;
  ave = average(a);

  for (integer i = 0; i < 3; i = i +1)
    write(ave.coord[i]);;

  write(ave.maxCoord());

  world world;

  // 5.4.3 objects, access to members of basic types
  // 5.4.5 objects, access to members of objecttypes, as well as the elements of the object
  world.default.r = 100;
  world.default.g = 100;
  world.default.b = 100;

  rgb new;
  integer temp;
  for (integer j = 0; j < 5; j = j +1) {
    if (a[j].coord[2] == 2) then {
      new = newrgb(255,255,255);
    } else {
      new = world.default;
    };
    temp = world.setAtPos(new, a[j]);
  };

  pos max;
  max = world.findBluest();
  for (integer k = 0; k < 3; k = k +1)
    write(max.coord[k]);;


  a[0].id = 0;
  a[1].id = 1;
  a[2].id = 2;
  a[3].id = 3;
  a[4].id = 4;
  write(-a[3].getid());

};