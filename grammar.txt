prog                      -> classDeclList #MakeNodeClassDeclList funcDefList #MakeNodeFuncDefList 'main' funcBody #MakeNodeMainFuncBody ';' #MakeNodeProg #EndProgram
classDecl                 -> 'class' 'id' #GetData #MakeNodeId inherListOp #MakeNodeInheritList '{' memberList '}' #MakeNodeMemberList ';' #MakeNodeClassDecl
classDeclList             -> classDecl classDeclList
classDeclList             -> EPSILON
funcDecl                  -> type 'id' #GetData #MakeNodeId '(' fParamList #MakeNodeFParamList ')' ';' #MakeNodeFuncDecl
funcHead                  -> type 'id' scopeOp '(' fParamList #MakeNodeFParamList ')'
scopeOp                   -> #GetData #MakeNodeType  'sr' 'id' #GetData #MakeNodeId
scopeOp                   -> #MakeNodeEpsilon #GetData #MakeNodeId
funcDef                   -> funcHead funcBody #MakeNodeFuncBody ';' #MakeNodeFuncDef
funcDefList               -> funcDef funcDefList
funcDefList               -> EPSILON
funcBody                  -> '{' statOrAssignOrVarDeclList '}'
memberDecl                -> type 'id' #GetData #MakeNodeId memberDecl'
memberDecl'               -> '(' fParamList #MakeNodeFParamList ')' ';' #MakeNodeFuncDecl
memberDecl'               -> dimList #MakeNodeDimList ';' #MakeNodeVarDecl
memberList                -> memberDecl memberList
memberList                -> EPSILON
numVarDecl                -> 'integer' #GetData #MakeNodeType 'id' #GetData #MakeNodeId dimList #MakeNodeDimList #MakeNodeVarDecl
numVarDecl                -> 'float' #GetData #MakeNodeType 'id' #GetData #MakeNodeId dimList #MakeNodeDimList #MakeNodeVarDecl
assignStat                -> variable assignOp expr
statement                 -> 'if'     '(' expr ')' 'then' statBlock 'else' statBlock #MakeNodeIfStat
statement                 -> 'for'    '(' type 'id' #GetData #MakeNodeId assignOp expr ';' relExpr ';' assignStat #MakeNodeAssignForStat ')' statBlock #MakeNodeForStat
statement                 -> 'read'   '(' variable ')' #MakeNodeReadStat
statement                 -> 'write'  '(' expr ')' #MakeNodeWriteStat
statement                 -> 'return' '(' expr ')' #MakeNodeReturnStat
statOrAssign              -> assignStat ';' #MakeNodeAssignStat
statOrAssign              -> statement  ';'
statOrAssignList          -> statOrAssign statOrAssignList
statOrAssignList          -> EPSILON
statOrAssignOrVarDecl     -> 'id' statOrAssignOrVarDecl'
statOrAssignOrVarDecl     -> numVarDecl ';'
statOrAssignOrVarDecl     -> statement ';'
statOrAssignOrVarDecl'    -> #GetData #MakeNodeType 'id' #GetData #MakeNodeId dimList #MakeNodeDimList #MakeNodeVarDecl ';'
statOrAssignOrVarDecl'    -> #GetData #MakeNodeId indexList #MakeNodeIndexList #MakeNodeDataMember idnestRep #MakeNodeVarElementList assignOp expr #MakeNodeAssignStat ';'
statOrAssignOrVarDeclList -> statOrAssignOrVarDecl statOrAssignOrVarDeclList
statOrAssignOrVarDeclList -> EPSILON
statBlock                 -> '{' statOrAssignList '}' #MakeNodeStatBlock
statBlock                 -> statOrAssign #MakeNodeStatBlock
statBlock                 -> EPSILON
expr                      -> arithExpr expr'
expr'                     -> relOp arithExpr #MakeNodeRelExpr
expr'                     -> EPSILON
relExpr                   -> arithExpr relOp arithExpr #MakeNodeRelExpr
arithExpr                 -> term arithExpr'
arithExpr'                -> addOp #GetData term arithExpr' #MakeNodeAddOp
arithExpr'                -> EPSILON
sign                      -> '+'
sign                      -> '-'
term                      -> factor term'
term'                     -> multOp #GetData factor term' #MakeNodeMultOp
term'                     -> EPSILON
factor                    -> 'id' #GetData #MakeNodeId factor' idnestRep #MakeNodeVarElementList
factor                    -> 'intNum' #GetData #MakeNodeNum
factor                    -> 'floatNum' #GetData #MakeNodeNum
factor                    -> '(' arithExpr ')'
factor                    -> 'not' #GetData factor #MakeNodeNot
factor                    -> sign #GetData factor #MakeNodeSign
factor'                   -> indexList #MakeNodeIndexList #MakeNodeDataMember
factor'                   -> '(' aParamList ')' #MakeNodeAParamList #MakeNodeFunctionCall
variable                  -> 'id' #GetData #MakeNodeId indexList #MakeNodeIndexList #MakeNodeDataMember idnestRep #MakeNodeVarElementList
idnest                    -> '.' 'id' #GetData #MakeNodeId idnest'
idnest'	                  -> indexList #MakeNodeIndexList #MakeNodeDataMember
idnest'                   -> '(' aParamList ')' #MakeNodeAParamList #MakeNodeFunctionCall
idnestRep                 -> idnest idnestRep
idnestRep                 -> EPSILON
inherListOp               -> ':' 'id' #GetData #MakeNodeIdi inherList
inherListOp               -> EPSILON
inherList                 -> ',' 'id' #GetData #MakeNodeIdi inherList
inherList                 -> EPSILON
index                     -> '[' arithExpr ']'
indexList                 -> index indexList
indexList                 -> EPSILON
dim                       -> '[' 'intNum' #GetData #MakeNodeNum ']'
dimList                   -> dim dimList
dimList                   -> EPSILON
type                      -> 'integer' #GetData #MakeNodeType
type                      -> 'float' #GetData #MakeNodeType
type                      -> 'id' #GetData #MakeNodeType
fParam                    -> type 'id' #GetData #MakeNodeId dimList #MakeNodeDimList #MakeNodeFParam
fParamList                -> fParam fParamList'
fParamList                -> EPSILON
fParamList'               -> ',' fParam fParamList'
fParamList'               -> EPSILON
aParam                    -> expr
aParamList                -> aParam aParamList'
aParamList                -> EPSILON
aParamList'               -> ',' aParam aParamList'
aParamList'               -> EPSILON
assignOp                  -> '='
relOp                     -> 'eq' #GetData #MakeNodeRelOp
relOp                     -> 'neq' #GetData #MakeNodeRelOp
relOp                     -> 'lt' #GetData #MakeNodeRelOp
relOp                     -> 'gt' #GetData #MakeNodeRelOp
relOp                     -> 'leq' #GetData #MakeNodeRelOp
relOp                     -> 'geq' #GetData #MakeNodeRelOp
addOp                     -> '+'
addOp                     -> '-'
addOp                     -> 'or'
multOp                    -> '*'
multOp                    -> '/'
multOp                    -> 'and'
