prog                      -> classDeclList #MakeNodeClassDeclList funcDefList #MakeNodeFuncDefList 'main' funcBody ';' #MakeNodeProg
classDecl                 -> 'class' 'id' #MakeNodeId inherListOp #MakeNodeInheritList '{' memberList '}' #MakeNodeMemberList ';' #MakeNodeClassDecl
classDeclList             -> classDecl classDeclList
classDeclList             -> EPSILON
funcDecl                  -> type 'id' #MakeNodeId '(' fParamList #MakeNodeFParamList ')' ';' #MakeNodeFuncDecl
funcHead                  -> type 'id' scopeOp #MakeNodeId '(' fParamList #MakeNodeFParamList ')'
scopeOp                   -> 'sr' 'id' #MakeNodeId
scopeOp                   -> #MakeNodeEpsilon scopeOp'
scopeOp'                  -> EPSILON
funcDef                   -> funcHead funcBody ';' #MakeNodeFuncDef
funcDefList               -> funcDef funcDefList
funcDefList               -> EPSILON
funcBody                  -> '{' statOrAssignOrVarDeclList '}' #MakeNodeStatBlock
memberDecl                -> type 'id' #MakeNodeId memberDecl'
memberDecl'               -> '(' fParamList #MakeNodeFParamList ')' ';' #MakeNodeFuncDecl
memberDecl'               -> dimList #MakeNodeDimList ';' #MakeNodeVarDecl
memberList                -> memberDecl memberList
memberList                -> EPSILON
numVarDecl                -> 'integer' #MakeNodeType 'id' #MakeNodeId dimList #MakeNodeDimList #MakeNodeVarDecl
numVarDecl                -> 'float' #MakeNodeType 'id' #MakeNodeId dimList #MakeNodeDimList #MakeNodeVarDecl
assignStat                -> variable assignOp expr
statement                 -> 'if'     '(' expr ')' 'then' statBlock 'else' statBlock #MakeNodeIfStat
statement                 -> 'for'    '(' type 'id' #MakeNodeId assignOp expr ';' relExpr ';' assignStat #MakeNodeAssignStat ')' statBlock #MakeNodeForStat
statement                 -> 'read'   '(' variable ')' #MakeNodeReadStat
statement                 -> 'write'  '(' expr ')' #MakeNodeWriteStat
statement                 -> 'return' '(' expr ')' #MakeNodeReturnStat
statOrAssign              -> assignStat ';' #MakeNodeAssignStati
statOrAssign              -> statement  ';'
statOrAssignList          -> statOrAssign statOrAssignList
statOrAssignList          -> EPSILON
statOrAssignOrVarDecl     -> 'id' statOrAssignOrVarDecl'
statOrAssignOrVarDecl     -> numVarDecl ';'
statOrAssignOrVarDecl     -> statement ';'
statOrAssignOrVarDecl'    -> #MakeNodeType 'id' #MakeNodeId dimList #MakeNodeDimList #MakeNodeVarDecl ';'
statOrAssignOrVarDecl'    -> #MakeNodeId indexList #MakeNodeIndexList #MakeNodeDataMember idnestRep #MakeNodeVarElementList assignOp expr #MakeNodeAssignStati ';'
statOrAssignOrVarDeclList -> statOrAssignOrVarDecl statOrAssignOrVarDeclList
statOrAssignOrVarDeclList -> EPSILON
statBlock                 -> '{' statOrAssignList '}' #MakeNodeStatBlock
statBlock                 -> statOrAssign #MakeNodeAssignStati #MakeNodeStatBlock
statBlock                 -> EPSILON
expr                      -> arithExpr expr'
expr'                     -> relOp arithExpr #MakeNodeRelExpr
expr'                     -> EPSILON
relExpr                   -> arithExpr relOp arithExpr #MakeNodeRelExpr
arithExpr                 -> term arithExpr'
arithExpr'                -> addOp term arithExpr' #MakeNodeAddOp
arithExpr'                -> EPSILON
sign                      -> '+'
sign                      -> '-'
term                      -> factor term'
term'                     -> multOp factor term' #MakeNodeMultOp
term'                     -> EPSILON
factor                    -> 'id' #MakeNodeId factor' idnestRep #MakeNodeVarElementList
factor                    -> 'intNum' #MakeNodeNum
factor                    -> 'floatNum' #MakeNodeNum
factor                    -> '(' arithExpr ')'
factor                    -> 'not' factor #MakeNodeNot
factor                    -> sign factor #MakeNodeSign
factor'                   -> indexList #MakeNodeIndexList #MakeNodeDataMember
factor'                   -> '(' aParamList ')' #MakeNodeAParamList #MakeNodeFunctionCall
variable                  -> 'id' #MakeNodeId indexList #MakeNodeIndexList #MakeNodeDataMember idnestRep #MakeNodeVarElementList
functionCall              -> 'id' #MakeNodeId '(' aParamList ')' #MakeNodeAParamList #MakeNodeFunctionCall idnestRep #MakeNodeVarElementList
idnest                    -> '.' 'id' #MakeNodeId idnest'
idnest'	                  -> indexList #MakeNodeIndexList #MakeNodeDataMember
idnest'                   -> '(' aParamList ')' #MakeNodeAParamList #MakeNodeFunctionCall
idnestRep                 -> idnest idnestRep
idnestRep                 -> EPSILON
inherListOp               -> ':' 'id' #MakeNodeIdi inherList
inherListOp               -> EPSILON
inherList                 -> ',' 'id' #MakeNodeIdi inherList
inherList                 -> EPSILON
index                     -> '[' arithExpr ']'
indexList                 -> index indexList
indexList                 -> EPSILON
dim                       -> '[' 'intNum' #MakeNodeNum ']'
dimList                   -> dim dimList
dimList                   -> EPSILON
type                      -> 'integer' #MakeNodeType
type                      -> 'float' #MakeNodeType
type                      -> 'id' #MakeNodeType
fParam                    -> type 'id' #MakeNodeId dimList #MakeNodeDimList #MakeNodeFParam
fParamList                -> fParam fParamList'
fParamList                -> EPSILON
fParamList'               -> ',' fParam fParamList'
fParamList'               -> EPSILON
aParam                    -> expr
aParamList                -> aParam aParamList'
aParamList                -> EPSILON
aParamList'               -> ',' aParam aParamList'
aParamList'               -> EPSILON
assignOp                  -> '='
relOp                     -> 'eq' #MakeNodeRelOp
relOp                     -> 'neq' #MakeNodeRelOp
relOp                     -> 'lt' #MakeNodeRelOp
relOp                     -> 'gt' #MakeNodeRelOp
relOp                     -> 'leq' #MakeNodeRelOp
relOp                     -> 'geq' #MakeNodeRelOp
addOp                     -> '+'
addOp                     -> '-'
addOp                     -> 'or'
multOp                    -> '*'
multOp                    -> '/'
multOp                    -> 'and'
